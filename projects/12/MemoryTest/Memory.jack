// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static Array static0;

    /** Initializes the class. */
    function void init() {
        let static0 = 0; // manually assign pointer value as zero
        // 2048 = first heap, 16383 = last heap
        let static0[2048] = 14334; // blockSize (size+2)
        let static0[2049] = 2050; // *nextPtr = &blockData
        // static0[2050] = 0; // &blockData (**nextPtr = null)
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return static0[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let static0[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array blockPtr;
        var int nextBlock;

        if (size < 0) {
            do Sys.error(5); // Allocated memory size must be positive
        }

        if (size = 0) {
            let size = 1; // min allocation is 1
        }

        let blockPtr = 2048; // start of heap

        // search from start of heap until suitable free block (first fit) or heap exhausted

        // 1st alloc: always false if size < maxSize (entire heap is freeBlock)
        // 2nd+ alloc: always true on first loop (initial blockSize == 0)
        // 2nd+ alloc: break when out of bounds or found a block with blockSize > size required

        // recycle: break when out of bounds or found a block with blockSize > size required
        // {2051} = 2 freeBlockSize (*blockPtr, *freeBlock)
        // {2052} = 2055 *freeNextPtr
        // {2053} = <freeBlockData>
        // {2054} = <freeBlockData>
        // {2055} = 0 allocBlockSize (*allocBlock)
        // {2056} = 2058 *allocNextPtr (**null)
        // {2057} = <allocBlockData>
        // {2058} = 14327 freeBlockSize2 (*freeBlock2)
        // {2059} = 2060 *freeNextPtr2 (**null)
        // {2060} = <freeBlockData2>

        while ((blockPtr < 16383) & (blockPtr[0] < size)) {
            let nextBlock = blockPtr[1];

            // if blockSize/nextBlockSize is zero or *nextBlock would exceed heap boundary
            if ((blockPtr[0] = 0) | (nextBlock > 16382) | (nextBlock[0] = 0)) {
                let blockPtr = nextBlock; // advance to next block and continue
            }

            // TODO
            else { // if blockSize/nextBlockSize is not zero (two adjacent free blocks)
                // update blockSize and *nextPtr

                // blockSize = (&blockData - *blockPtr) + nextBlockSize
                let blockPtr[0] = (blockPtr[1] - blockPtr) + nextBlock[0];

                // if **(nextPtr+1) == &blockData
                if (nextBlock[1] = (nextBlock + 2)) {
                    // *nextPtr = &blockData+2
                    let blockPtr[1] = blockPtr + 2;
                }
                else {
                    // *nextPtr = *nextBlockPtr
                    let blockPtr[1] = nextBlock[1];
                }
            }
        }

        // error if loop was broken by exhausting heap / going out of bounds
        // minimum (3 + size) words required to continue (blockSize, *nextPtr, freeBlockSize)
        if ((blockPtr + size) > 16379) {
            do Sys.error(6); // Heap overflow
        }
        
        // if blockSize is sufficient split into allocBlock and freeBlock
        if (blockPtr[0] > (size + 2)) {           
            // freeBlockSize = blockSize - allocBlockSize
            let blockPtr[size + 2] = blockPtr[0] - size - 2;

            // TODO: what case causes it to be updated?
            // if *allocNextPtr == &allocBlockData (i.e. *allocNextPtr has not been updated)
            if (blockPtr[1] = (blockPtr + 2)) {
                // *freeNextPtr = &freeBlockData (**null)
                let blockPtr[size + 3] = blockPtr + size + 4;
            }
            else {
                // *freeNextPtr = *allocNextPtr
                let blockPtr[size + 3] = blockPtr[1];
            }

            // *allocNextPtr = *freeBlock
            let blockPtr[1] = blockPtr + size + 2;
        }

        let blockPtr[0] = 0; // allocBlockSize = 0, flagging it as allocated
        return blockPtr + 2; // return &allocBlockData
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations (appended to end of freeList) */
    function void deAlloc(Array o) {
        var Array blockPtr;
        var int nextPtr;

        let blockPtr = o - 2; // move backwards from &allocBlockData to start of *allocBlock header
        let nextPtr = blockPtr[1]; // *nextPtr

        // if *nextPtr references an allocated block update current block in place as free
        if (nextPtr[0] = 0) {
            // restore allocBlockSize
            let blockPtr[0] = blockPtr[1] - blockPtr - 2;
        }

        // elif *nextPtr references a free block combine it with this one (defrag)
        else {
            // size = what is required for current block + total size of the adjacent free block
            let blockPtr[0] = blockPtr[1] - blockPtr + nextPtr[0];

            // update *nextPtr to reference the block after the one just combined - flink or data (terminate)

            // if nextBlock was at end of freeList continue to reference freeBlockData to terminate the list
            if (nextPtr[1] = (nextPtr + 2)) {
                let blockPtr[1] = blockPtr + 2;
            }

            // if nextBlock was another freed block
            // set it to the *blockPtr of the block following it to link the entries
            else {
                let blockPtr[1] = nextPtr[1];
            }
        }
        return;
    }
}
